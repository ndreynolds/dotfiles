" .vimrc
" Author:     Nick Reynolds <ndreynolds@gmail.com>
" Repository: http://github.com/ndreynolds/dotfiles


" Vundle & Plugins ---------------------------------------------- {{{ 

set shell=/bin/sh
set nocompatible
runtime! macros/matchit.vim
filetype off

let s:needs_vundle = !isdirectory($HOME . '/.vim/bundle/vundle')
if s:needs_vundle
  echo "-------------------------------"
  echo "Installing Vundle... Sit tight."
  echo "-------------------------------\n"
  silent !mkdir -p ~/.vim/bundle
  silent !git clone https://github.com/gmarik/vundle ~/.vim/bundle/vundle
endif

if v:version < 703
  let g:loaded_youcompleteme = 1
endif

set rtp+=~/.vim/bundle/vundle/
call vundle#rc()

Bundle 'gmarik/vundle'

Bundle 'aaronbieber/quicktask'
Bundle 'altercation/vim-colors-solarized'
Bundle 'bling/vim-airline'
Bundle 'briancollins/vim-jst'
Bundle 'chrisbra/csv.vim'
Bundle 'dag/vim-fish'
Bundle 'danchoi/ri.vim'
Bundle 'digitaltoad/vim-jade'
Bundle 'eagletmt/neco-ghc'
Bundle 'ecomba/vim-ruby-refactoring'
Bundle 'godlygeek/tabular'
Bundle 'groenewege/vim-less'
Bundle 'honza/vim-snippets'
Bundle 'jcf/vim-latex'
Bundle 'kana/vim-textobj-user'
Bundle 'kchmck/vim-coffee-script'
Bundle 'kien/ctrlp.vim'
Bundle 'lokaltog/vim-easymotion'
Bundle 'lucapette/vim-ruby-doc'
Bundle 'lukaszb/vim-web-indent'
Bundle 'mattn/gist-vim'
Bundle 'mattn/webapi-vim'
Bundle 'nelstrom/vim-textobj-rubyblock'
Bundle 'othree/xml.vim'
Bundle 'scrooloose/nerdtree'
Bundle 'scrooloose/syntastic'
Bundle 'sirver/ultisnips'
Bundle 'sjl/gundo.vim'
Bundle 'tpope/vim-abolish'
Bundle 'tpope/vim-commentary'
Bundle 'tpope/vim-dispatch'
Bundle 'tpope/vim-endwise'
Bundle 'tpope/vim-eunuch'
Bundle 'tpope/vim-fugitive'
Bundle 'tpope/vim-haml'
Bundle 'tpope/vim-markdown'
Bundle 'tpope/vim-rails'
Bundle 'tpope/vim-surround'
Bundle 'tpope/vim-vinegar'
Bundle 'tristen/vim-sparkup'
Bundle 'valloric/YouCompleteMe'
Bundle 'vim-pandoc/vim-pandoc'
Bundle 'vim-ruby/vim-ruby'
Bundle 'vim-scripts/LanguageTool'
Bundle 'vim-scripts/renamer.vim'
Bundle 'vim-scripts/slimv.vim'
Bundle 'wavded/vim-stylus'

filetype plugin indent on

if s:needs_vundle
  :BundleInstall
endif

" }}}


" Options ------------------------------------------------------- {{{

syntax on
set number
set tabstop=4
set expandtab
set shiftwidth=4

set encoding=utf-8
set scrolloff=3
set autoindent
set showmode
set showcmd
set hidden
set wildmenu
set wildmode=longest:full,full

set wildignore+=.git,.svn,.hg,.pyc,.sw?
set cursorline
set ttyfast
set ruler
set backspace=indent,eol,start
set laststatus=2
set mouse=a
set iskeyword+=\-

set ignorecase
set smartcase
set incsearch
set showmatch
set hlsearch
set foldmethod=marker

" Phonetic Cyrillic keyboard mapping
set langmap=АБЦДЕФГЧИЙКЛМНОПЯРСТУВШХЫЗ;ABCDEFGHIJKLMNOPQRSTUVWXYZ,абцдефгчийклмнопярстувшхыз;abcdefghijklmnopqrstuvwxyz

" Store swap, backup and undo files within .vim/ 
" Make the directories as needed.

if !isdirectory($HOME . '/.vim/swap')
  :silent !mkdir -p ~/.vim/swap > /dev/null 2>&1
endif
set dir=~/.vim/swap

if !isdirectory($HOME . '/.vim/backup')
  :silent !mkdir -p ~/.vim/backup > /dev/null 2>&1
endif
set backupdir=~/.vim/backup
set backup

if exists("+undofile")
  if !isdirectory($HOME . '/.vim/undo')
    :silent !mkdir -p ~/.vim/undo > /dev/null 2>&1
  endif
  set undodir=~/.vim/undo
  set undofile
endif

" }}}


" Plugin Configuration ------------------------------------------- {{{

let g:syntastic_javascript_checker = 'jshint'
let g:syntastic_ruby_checkers = ['rubocop', 'mri']
let g:syntastic_ruby_exec = '~/.rbenv/shims/ruby'

let g:tex_flavor = 'latex'

let g:gist_open_browser_after_post = 1


" Make UltiSnips play nice with YCM.
" See here: https://github.com/Valloric/YouCompleteMe/issues/36
let g:UltiSnipsExpandTrigger       = "<tab>"
let g:UltiSnipsJumpForwardTrigger  = "<tab>"
let g:UltiSnipsJumpBackwardTrigger = "<s-tab>"
let g:UltiSnipsSnippetDirectories  = ["snips"]

function! g:UltiSnips_Complete()
    call UltiSnips#ExpandSnippet()
    if g:ulti_expand_res == 0
        if pumvisible()
            return "\<C-n>"
        else
            call UltiSnips#JumpForwards()
            if g:ulti_jump_forwards_res == 0
               return "\<TAB>"
            endif
        endif
    endif
    return ""
endfunction

au InsertEnter * exec "inoremap <silent> " . g:UltiSnipsExpandTrigger . " <C-R>=g:UltiSnips_Complete()<cr>"


" LanguageTool
let jar = '/usr/local/Cellar/languagetool/2.3/libexec/languagetool-commandline.jar'
if filereadable(jar)
  let g:languagetool_jar = jar
endif
let g:languagetool_disable_rules = 'WHITESPACE_RULE,EN_QUOTES,TYPOGRAFISCHE_ANFUEHRUNGSZEICHEN'

" Slimv servers
" if has('unix') && system('uname') =~ 'Darwin'
"   function! BuildTermCmd(cmd)
"     return '!osascript -e "tell application \"Terminal\" to do script \"' . a:cmd . '\""'
"   endfunction

"   "let g:slimv_swank_cmd = BuildTermCmd('sbcl --load ~/.vim/bundle/slimv/slime/start-swank.lisp')
"   "let g:slimv_swank_scheme = BuildTermCmd("scheme --eval '(let loop () (start-swank) (loop))'")
" endif

" airline
if $POWERLINE_FANCY
  let g:airline_powerline_fonts=1
endif

" Set git exec path 
"
" `push.default = simple` throws an error with Apple Git 1.7
" Try to find and use 1.8
if filereadable('/usr/local/bin/git')
  let g:fugitive_git_executable = '/usr/local/bin/git'
endif


" Solarized colorscheme
"
" Only active if within the matching $ITERM_PROFILE.
if $ITERM_PROFILE == 'SolarizedLight' || $ITERM_PROFILE == 'SolarizedDark'
  if $ITERM_PROFILE == 'SolarizedLight'
    set background=light
  elseif $ITERM_PROFILE == 'SolarizedDark'
    set background=dark
  endif

  syntax enable
  colorscheme solarized
  set t_Co=16
endif

if $ITERM_PROFILE == 'Darkside'
  syntax enable
  colorscheme molokai
  set t_Co=256
endif


" The Silver Searcher
if executable('ag')
  set grepprg=ag\ --nogroup\ --nocolor
  let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'
  let g:ctrlp_use_caching = 0
endif


let NERDTreeMinimalUI = 1
let NERDTreeDirArrows = 1
let NERDTreeHighlightCursorLine = 1

let g:necoghc_enable_detailed_browse = 1

let g:ycm_collect_identifiers_from_comments_and_strings = 1
let g:ycm_collect_identifiers_from_tags_files = 1

let g:ruby_doc_command='open'

" let g:startify_custom_header =
"       \ map(split(system('$SHELL -c "vim --version | head -2"'), '\n'), '"   ". v:val') + ['','']

" let g:startify_custom_footer = ['',''] +
"       \ map(split(system('fortune -n 500 -s | cowsay'), '\n'), '"   ". v:val') + ['','']


" }}}


" Mappings ------------------------------------------------------- {{{

" Set leader to ,
let mapleader = ","
let maplocalleader = ","

" jj has same behavior as <ESC>
inoremap jj <ESC>

" Make Y behave like C & D
nnoremap Y y$

" Shortcut for the directory of the current file
cnoremap %/ <c-r>=expand('%:p:h')<CR>/

" Clear search highlighting
nnoremap <silent> <c-l> :nohl<CR><c-l>

" Arrow keys move at turbo speed
" (Off until I stop using them to move around files)
" nnoremap <left>  4h
" nnoremap <down>  4j
" nnoremap <up>    4k
" nnoremap <right> 4l
" vnoremap <left>  4h
" vnoremap <down>  4j
" vnoremap <up>    4k
" vnoremap <right> 4l

" Stop vim-latex from stealing <c-j> 
nnoremap <leader>m <Plug>IMAP_JumpForward

" CTRL-{j,k,l,h} to switch windows
nnoremap <c-j> <c-w>j
nnoremap <c-k> <c-w>k
nnoremap <c-l> <c-w>l 
nnoremap <c-h> <c-w>h 

" Catch accidental shift keys
command! -bang -nargs=* -complete=file E e<bang> <args>
command! -bang -nargs=* -complete=file W w<bang> <args>
command! -bang -nargs=* -complete=file Wq wq<bang> <args>
command! -bang -nargs=* -complete=file Wqa wqa<bang> <args>
command! -bang -nargs=* -complete=file WQ wq<bang> <args>
command! -bang -nargs=* -complete=file Q q<bang> <args>
command! -bang -nargs=* -complete=file Vsp vsp<bang> <args>

" No-shift-key-required alternatives
nnoremap <leader>w :w<cr>
nnoremap <leader>wq :wq<cr>

" Open or close the NERDTree split
nnoremap <F5> :NERDTreeToggle<cr>

" Open or close the Gundo split
nnoremap <F6> :GundoToggle<cr>

" vim-cakephp shortcuts
nnoremap <leader>cv :Cview<cr>
nnoremap <leader>cc :Ccontroller<cr>
nnoremap <leader>cm :Cmodel<cr>

" vim-rails shortcuts
nnoremap <leader>rv :Rview<cr>
nnoremap <leader>rc :Rcontroller<cr>
nnoremap <leader>rm :Rmodel<cr>

" fugitive shortcuts
nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gs :Gstatus<cr>
nnoremap <leader>gw :Gwrite<cr>
nnoremap <leader>ga :Gadd<cr>
" (gc is used by TComment)
nnoremap <leader>go :Gcheckout<cr> 
nnoremap <leader>gm :Gmove<cr>
nnoremap <leader>gr :Gremove<cr>
nnoremap <leader>gh :Gbrowse<cr>

" EasyMotion
let g:EasyMotion_leader_key = '<leader><space>'

" Reflow text to 80 cols
nnoremap <leader>sw :set textwidth=80<cr>:normal! gggqG<esc>

" Give the buffer a header comment
nnoremap <leader>hc :call HeaderComment()<cr>

" Write ROs as root
cnoremap w!! w !sudo tee % > /dev/null

" Write file with 'eventignore' set.
cnoremap w-- noautocmd w

" Toggle spellcheck
nnoremap <leader>sp :set spell!<cr>

" Edit vimrc
nnoremap <leader>ev :e $MYVIMRC<cr>

" Source vimrc
nnoremap <leader>sv :source $MYVIMRC<cr>

" Set current directory to directory of file
nnoremap <leader>cd :lcd %:p:h<cr>:pwd<cr>

" Quick open some URLs.
nnoremap <leader>ox :OpenURLAtCursor<cr>
vnoremap <leader>ox :OpenURLAtCursor<cr>
nnoremap <leader>og :OpenURL google.com<cr>
nnoremap <leader>om :OpenURL mail.google.com<cr>
nnoremap <leader>oh :OpenURL github.com<cr>
nnoremap <leader>on :OpenURL news.ycombinator.com<cr>
nnoremap <leader>oc :OpenURL my.cl.ly<cr>
nnoremap <leader>os :OpenURL grooveshark.com<cr>
nnoremap <leader>ov :OpenURL tnerual.eriogerg.free.fr/vimqrc.html<cr>
nnoremap <leader>ol :OpenURL localhost<cr>

" Show definition of word under cursor
nnoremap <leader>d :call OpenURL("dict.cc/?s=" . expand("<cword>"))<cr>

" Hacky clipboard workaround for Mac if compiled without clipboard support
if !has("clipboard") && system("uname") =~ "Darwin"
  vnoremap "+y !pbcopy<cr>u
endif

" Open scratchpad
nnoremap <leader>p :set paste!<cr>

" ctrlp
nnoremap <leader><leader> :CtrlP<cr>
nnoremap <leader>n :CtrlP ~/repos/notes<cr>
nnoremap <leader>k :CtrlP ~/repos/anki-imports<cr>
nnoremap <leader>t :CtrlPTag<cr>

" Pretty print JSON and XML
nnoremap <leader>j :%!python -m json.tool<cr>
vnoremap <leader>j :!python -m json.tool<cr>
nnoremap <leader>x :%!tidy -i -q -asxhtml -utf8<cr>
vnoremap <leader>x :!tidy -i -q -xml -utf8<cr>

" Underline a heading
nnoremap <leader>u yyp<c-v>$r-

" * and # in visual mode search for the selection (from Practical Vim)
xnoremap * :<c-u>call <sid>VSetSearch()<cr>/<c-r>=@/<cr><cr> 
xnoremap # :<c-u>call <sid>VSetSearch()<cr>?<c-r>=@/<cr><cr>

" Tabularize shortcuts
nnoremap <leader>a= :Tabularize /=<cr>
vnoremap <leader>a= :Tabularize /=<cr>
nnoremap <leader>a: :Tabularize /:\zs<cr>
vnoremap <leader>a: :Tabularize /:\zs<cr>
nnoremap <leader>ac :Tabularize /,\zs\+/<cr>
vnoremap <leader>ac :Tabularize /,\zs\+/<cr>

" Rspec with Dispatch
nnoremap <leader>r :Dispatch rspec %<cr>

" bind K to grep word under cursor
nnoremap K :grep! "\b<C-R><C-W>\b"<CR>:cw<CR>

" Generate HTML with Pandoc and open in browser 
nnoremap <leader>h :call PreviewMarkdown()<cr>

" }}}


" Functions ------------------------------------------------------ {{{

" Open my scratchpad
function! s:OpenScratchpad()
  if $SCRATCHPAD
    exe ":Gist " . $SCRATCHPAD
    au! BufLeave,FocusLost * silent! wall
  else
    echoe "No $SCRATCHPAD"
  endif
endfunction
command! OpenScratchpad call s:OpenScratchpad()

" Prepend a header comment with the filename and a dashed line.
function! HeaderComment()
  " Prepend the buffer with filename and a dashed line.
  call append(0, [expand('%:t'), repeat('-', strlen(expand('%:t')))])
  " Comment it out with TComment
  norm ggVjgc
endfunction

" Open a URL using a system-appropriate opener
function! OpenURL(url)
  let url = a:url
  if url !~ "http://"
    let url = "http://" . url
  endif

  " Mac?
  if system("uname") =~ "Darwin" " Mac ?
    silent call system('open "' . url . '" &')
  " Windows?
  elseif has("win32") || has("win64")
    silent call system("start " . url)
  " Some *nix? Does it have xdg-open?
  elseif has("unix") && executable("xdg-open")
    silent call system("xdg-open " . url . " &")
  else
    echoe "Couldn't find a suitable url opener."
  endif

  redraw!
endfunction
command! -nargs=1 OpenURL call OpenURL(<f-args>)

" Open a URL that's under the cursor or visually selected
function! OpenURLAtCursor()
  if mode() ==# 'n'
    let uri = expand("<cWORD>")
    if match(uri, "://")
      let uri = expand("<cfile>")
    endif
  else
    let uri = s:GetSelectedText()
  endif

  call OpenURL(uri)
endfunction
command! -bar OpenURLAtCursor call OpenURLAtCursor(<f-args>)

function! PreviewMarkdown()
  let tempdir = substitute(system("mktemp -d /tmp/vim-md.XXXX"), "\n", "", "")
  let tempfile = tempdir . "/" . expand("%:t:r") . ".html"
  let convertCmd = "pandoc -o " . tempfile . " -c ~/repos/scratchpad/style.css " . expand("%")
  let openCmd = "open " .  tempfile
  silent call system(convertCmd . ";" . openCmd)
endfunction

function! s:GetSelectedText()
  let [lnum1, col1] = getpos("'<")[1:2]
  let [lnum2, col2] = getpos("'>")[1:2]
  let lines = getline(lnum1, lnum2)
  let lines[-1] = lines[-1][: col2 - (&selection == 'inclusive' ? 1 : 2)]
  let lines[0] = lines[0][col1 - 1:]
  return join(lines, "\n")
endfunction

function! s:VSetSearch()
  let temp = @s
  norm! gv"sy
  let @/ = '\V' . substitute(escape(@s, '/\'), '\n', '\\n', 'g') 
  let @s = temp
endfunction

function! s:ReloadChrome()
  silent call system("osascript ~/dotfiles/bin/reload-chrome.scpt &")
endfunction

" }}}


" Statusline ----------------------------------------------------- {{{

set statusline=[%n]\ %<%.99f\ %h%w%m%r%{fugitive#statusline()}%{SyntasticStatuslineFlag()}%{exists('*CapsLockStatusline')?CapsLockStatusline():''}%y%=%-16(\ %l,%c-%v\ %)%P

" }}}


" Misc ----------------------------------------------------------- {{{

" Highlight lines longer than 80 chars
highlight OverLength ctermbg=red ctermfg=white guibg=#592929
match OverLength /\%81v.\+/

" VCS conflict markers
match ErrorMsg '^\(<\|=\|>\)\{7\}\([^=].\+\)\?$'

" }}}


" Abbreviations -------------------------------------------------- {{{

iabbrev @@ ndreynolds@gmail.com
iabbrev ndr Nick Reynolds
iabbrev <expr> dts strftime("%c")

" }}}


" Event Autocommands --------------------------------------------- {{{

" Resize splits on window resize
au VimResized * :wincmd =

" Set syntax for a few template extensions
au BufNewFile,BufRead *.jst set syntax=jst
au BufNewFile,BufRead *.json set filetype=json
au BufNewFile,BufRead *.tpl set filetype=html.twig

au BufNewFile,BufRead *.txt set filetype=markdown

" JSON
au BufNewFile,BufRead *.json set filetype=javascript

" Header files should assume C (most likely for me)
au BufNewFile,BufRead *.h set filetype=c

if executable("osascript") && exists('g:autoloaded_rails')
  au BufWritePost * call s:ReloadChrome()
endif

au QuickFixCmdPost *grep* cwindow


" }}}


" FileType Autocommands ------------------------------------------ {{{

augroup FTOptions
  autocmd Filetype python                   setlocal ai et sta sw=4 sts=4 tw=79
  autocmd Filetype cpp,cs,java              setlocal ai et sta sw=4 sts=4 cin
  autocmd Filetype c,ruby,coffee,javascript setlocal ai et sta sw=2 sts=2
  autocmd Filetype css,scss,less            setlocal ai et sta sw=2 sts=2
  autocmd Filetype jst,eruby,eco,haml       setlocal ai et sta sw=2 sts=2
  autocmd Filetype html,xhtml               setlocal ai et sta sw=2 sts=2
  autocmd Filetype html.twig,jade           setlocal ai et sta sw=2 sts=2
  autocmd Filetype eruby,yaml,json          setlocal ai et sta sw=2 sts=2
  autocmd Filetype cucumber                 setlocal ai et sta sw=2 sts=2
  autocmd Filetype vim,markdown             setlocal ai et sta sw=2 sts=2
  autocmd Filetype sh,bash,zsh              setlocal ai et sta sw=2 sts=2
  autocmd FileType gitcommit,markdown       setlocal spell
  autocmd FileType haskell                  setlocal omnifunc=necoghc#omnifunc
  autocmd FileType crontab                  set nobackup nowritebackup
  autocmd FileType help                     nnoremap <silent><buffer> q :q<cr>

  autocmd FileType ruby,eruby set omnifunc=rubycomplete#Complete
  autocmd FileType ruby,eruby let g:rubycomplete_buffer_loading = 1 
  autocmd FileType ruby,eruby let g:rubycomplete_classes_in_global = 1
  autocmd FileType ruby,eruby let g:rubycomplete_rails = 1
augroup END

" }}}
