" .vimrc
" Author:     Nick Reynolds <ndreynolds@gmail.com>
" Repository: http://github.com/ndreynolds/dotfiles


" Plugins ------------------------------------------------------- {{{ 

set shell=/bin/sh
set nocompatible
runtime! macros/matchit.vim
filetype off

let s:needs_vimplug = !filereadable($HOME . '/.vim/autoload/plug.vim')
if s:needs_vimplug
  echo "---------------------------------"
  echo "Installing vim-plug... Sit tight."
  echo "-------------------------------\n"
  silent !mkdir -p ~/.vim/autoload
  silent !curl -fLo ~/.vim/autoload/plug.vim https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
endif

if v:version < 703
  let g:loaded_youcompleteme = 1
endif

call plug#begin('~/.vim/plugged')


" Language Support ---------------------------------------------

Plug 'briancollins/vim-jst'
Plug 'dag/vim-fish'
Plug 'digitaltoad/vim-jade'
Plug 'ecomba/vim-ruby-refactoring'
Plug 'groenewege/vim-less'
Plug 'jcf/vim-latex'
Plug 'kchmck/vim-coffee-script'
Plug 'lukaszb/vim-web-indent'
Plug 'othree/xml.vim'
Plug 'tpope/vim-haml'
Plug 'tpope/vim-markdown'
Plug 'wavded/vim-stylus'
"Plug 'chrisbra/csv.vim'
Plug 'eagletmt/neco-ghc'
Plug 'vim-pandoc/vim-pandoc'
Plug 'vim-pandoc/vim-pandoc-syntax'
Plug 'vim-ruby/vim-ruby'
Plug 'wting/rust.vim'

" Colorschemes / Visual ----------------------------------------

Plug 'altercation/vim-colors-solarized'
Plug 'bling/vim-airline'

" Motions / Text Objects / Mappings ----------------------------

"Plug 'bkad/CamelCaseMotion'
Plug 'godlygeek/tabular'
Plug 'nelstrom/vim-textobj-rubyblock'
Plug 'lokaltog/vim-easymotion'
Plug 'kana/vim-textobj-user'
Plug 'tpope/vim-abolish'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-endwise'
Plug 'tpope/vim-surround'

" Utilities ----------------------------------------------------

Plug 'honza/vim-snippets'
Plug 'kien/ctrlp.vim'
" Plug 'mattn/gist-vim'
" Plug 'mattn/webapi-vim'
Plug 'scrooloose/syntastic'
Plug 'sirver/ultisnips'
Plug 'sjl/gundo.vim'
Plug 'tpope/vim-dispatch'
Plug 'tpope/vim-eunuch'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-rails'
"Plug 'tpope/vim-rhubarb'
Plug 'tpope/vim-rsi'
Plug 'tpope/vim-vinegar'
Plug 'valloric/YouCompleteMe', { 'do': './install.sh --clang-completer' }
Plug 'vim-scripts/LanguageTool'
Plug 'vim-scripts/renamer.vim'

call plug#end()

if s:needs_vimplug
  :PlugInstall
endif

" }}}


" Options ------------------------------------------------------- {{{

syntax on
set number
set tabstop=4
set expandtab
set shiftwidth=4

set encoding=utf-8
set scrolloff=3
set autoindent
set showmode
set showcmd
set hidden
set wildmenu
set wildmode=longest:full,full

set wildignore+=.git,.svn,.hg,.pyc,.sw?
set cursorline
set ttyfast
set ruler
set backspace=indent,eol,start
set laststatus=2
set mouse=a
set iskeyword+=\-

set ignorecase
set smartcase
set incsearch
set showmatch
set hlsearch
set foldmethod=marker

" Phonetic Cyrillic keyboard mapping
set langmap=АБЦДЕФГЧИЙКЛМНОПЯРСТУВШХЫЗ;ABCDEFGHIJKLMNOPQRSTUVWXYZ,абцдефгчийклмнопярстувшхыз;abcdefghijklmnopqrstuvwxyz

" Store swap, backup and undo files within .vim/ 
" Make the directories as needed.

if !isdirectory($HOME . '/.vim/swap')
  :silent !mkdir -p ~/.vim/swap > /dev/null 2>&1
endif
set dir=~/.vim/swap

if !isdirectory($HOME . '/.vim/backup')
  :silent !mkdir -p ~/.vim/backup > /dev/null 2>&1
endif
set backupdir=~/.vim/backup
set backup

if exists("+undofile")
  if !isdirectory($HOME . '/.vim/undo')
    :silent !mkdir -p ~/.vim/undo > /dev/null 2>&1
  endif
  set undodir=~/.vim/undo
  set undofile
endif

" }}}


" Plugin Configuration ------------------------------------------- {{{

let g:syntastic_javascript_checker = 'jshint'
let g:syntastic_ruby_checkers = ['rubocop', 'mri']
let g:syntastic_ruby_exec = '~/.rbenv/shims/ruby'

let g:tex_flavor = 'latex'

let g:gist_open_browser_after_post = 1

let g:UltiSnipsExpandTrigger="<c-j>"
let g:UltiSnipsJumpForwardTrigger="<c-b>"
let g:UltiSnipsJumpBackwardTrigger="<c-z>"
let g:UltiSnipsEditSplit="vertical"

let g:rsi_no_meta = 1

" LanguageTool
let jar = '/usr/local/Cellar/languagetool/2.3/libexec/languagetool-commandline.jar'
if filereadable(jar)
  let g:languagetool_jar = jar
endif
let g:languagetool_disable_rules = 'WHITESPACE_RULE,EN_QUOTES,TYPOGRAFISCHE_ANFUEHRUNGSZEICHEN'

" airline
if $POWERLINE_FANCY
  let g:airline_powerline_fonts=1
endif

if $COLORSCHEME == "solarized"
  syntax enable
  set background=dark
  colorscheme solarized
endif

" The Silver Searcher
if executable('ag')
  set grepprg=ag\ --nogroup\ --nocolor
  let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'
  let g:ctrlp_use_caching = 0
endif

let g:necoghc_enable_detailed_browse = 1

let g:ycm_collect_identifiers_from_comments_and_strings = 1
let g:ycm_collect_identifiers_from_tags_files = 1

let g:ruby_doc_command='open'


" }}}


" Mappings ------------------------------------------------------- {{{

" Set leader to ,
let mapleader = ","
let maplocalleader = ","

" jj has same behavior as <ESC>
inoremap jj <ESC>

" Make Y behave like C & D
nnoremap Y y$

" Shortcut for the directory of the current file
cnoremap %/ <c-r>=expand('%:p:h')<CR>/

" Clear search highlighting
nnoremap <silent> <c-l> :nohl<CR><c-l>

" Arrow keys move at turbo speed
" (Off until I stop using them to move around files)
" nnoremap <left>  4h
" nnoremap <down>  4j
" nnoremap <up>    4k
" nnoremap <right> 4l
" vnoremap <left>  4h
" vnoremap <down>  4j
" vnoremap <up>    4k
" vnoremap <right> 4l

" Stop vim-latex from stealing <c-j> 
nnoremap <leader>m <Plug>IMAP_JumpForward

" CTRL-{j,k,l,h} to switch windows
nnoremap <c-j> <c-w>j
nnoremap <c-k> <c-w>k
nnoremap <c-l> <c-w>l 
nnoremap <c-h> <c-w>h 

" Catch accidental shift keys
command! -bang -nargs=* -complete=file E e<bang> <args>
command! -bang -nargs=* -complete=file W w<bang> <args>
command! -bang -nargs=* -complete=file Wq wq<bang> <args>
command! -bang -nargs=* -complete=file Wqa wqa<bang> <args>
command! -bang -nargs=* -complete=file WQ wq<bang> <args>
command! -bang -nargs=* -complete=file Q q<bang> <args>
command! -bang -nargs=* -complete=file Vsp vsp<bang> <args>

" Open or close the Gundo split
nnoremap <F6> :GundoToggle<cr>

" vim-rails shortcuts
nnoremap <leader>rv :Rview<cr>
nnoremap <leader>rc :Rcontroller<cr>
nnoremap <leader>rm :Rmodel<cr>

" fugitive shortcuts
nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gs :Gstatus<cr>
nnoremap <leader>gw :Gwrite<cr>
nnoremap <leader>ga :Gadd<cr>
" (gc is used by TComment)
nnoremap <leader>go :Gcheckout<cr> 
nnoremap <leader>gm :Gmove<cr>
nnoremap <leader>gr :Gremove<cr>
nnoremap <leader>gh :Gbrowse<cr>

" EasyMotion
let g:EasyMotion_leader_key = '<leader><space>'

" Reflow text to 80 cols
nnoremap <leader>sw :set textwidth=80<cr>:normal! gggqG<esc>

" Give the buffer a header comment
nnoremap <leader>hc :call HeaderComment()<cr>

" Write file with 'eventignore' set.
cnoremap w-- noautocmd w

" Toggle spellcheck
nnoremap <leader>sp :set spell!<cr>

" Edit vimrc
nnoremap <leader>ev :e $MYVIMRC<cr>

" Source vimrc
nnoremap <leader>sv :source $MYVIMRC<cr>

" Quick open some URLs.
nnoremap <leader>ox :OpenURLAtCursor<cr>
vnoremap <leader>ox :OpenURLAtCursor<cr>
nnoremap <leader>og :OpenURL google.com<cr>
nnoremap <leader>om :OpenURL mail.google.com<cr>
nnoremap <leader>oh :OpenURL github.com<cr>
nnoremap <leader>ol :OpenURL localhost<cr>

" Show definition of word under cursor
nnoremap <leader>d :call OpenURL("dict.cc/?s=" . expand("<cword>"))<cr>

" Hacky clipboard workaround for Mac if compiled without clipboard support
if !has("clipboard") && system("uname") =~ "Darwin"
  vnoremap "+y !pbcopy<cr>u
endif

" Open scratchpad
nnoremap <leader>p :set paste!<cr>

" ctrlp
nnoremap <leader><leader> :CtrlP<cr>
nnoremap <leader>n :CtrlP ~/repos/notes<cr>
nnoremap <leader>k :CtrlP ~/repos/anki-imports<cr>
nnoremap <leader>t :CtrlPTag<cr>

" Pretty print JSON and XML
nnoremap <leader>j :%!python -m json.tool<cr>
vnoremap <leader>j :!python -m json.tool<cr>
nnoremap <leader>x :%!tidy -i -q -asxhtml -utf8<cr>
vnoremap <leader>x :!tidy -i -q -xml -utf8<cr>

" Underline a heading
nnoremap <leader>u yyp<c-v>$r-

" * and # in visual mode search for the selection (from Practical Vim)
xnoremap * :<c-u>call <sid>VSetSearch()<cr>/<c-r>=@/<cr><cr> 
xnoremap # :<c-u>call <sid>VSetSearch()<cr>?<c-r>=@/<cr><cr>

" Tabularize shortcuts
nnoremap <leader>a= :Tabularize /=<cr>
vnoremap <leader>a= :Tabularize /=<cr>
nnoremap <leader>a: :Tabularize /:\zs<cr>
vnoremap <leader>a: :Tabularize /:\zs<cr>
nnoremap <leader>ac :Tabularize /,\zs\+/<cr>
vnoremap <leader>ac :Tabularize /,\zs\+/<cr>

" Rspec with Dispatch
nnoremap <leader>r :Dispatch rspec %<cr>

" bind K to grep word under cursor
nnoremap K :grep! "\b<C-R><C-W>\b"<CR>:cw<CR>

" Generate HTML with Pandoc and open in browser 
nnoremap <leader>h :call PreviewMarkdown()<cr>

" <ctrl-c> to redraw screen
nnoremap <c-c> :redraw!<cr>

" Changes :x => y to x: y where possible
command! -range RubyUpdateHashSyntax :exe '<line1>,<line2>s/:\([^ ]*\)\(\s*\)=>/\1:/g'

" }}}


" Functions ------------------------------------------------------ {{{

" Open my scratchpad
function! s:OpenScratchpad()
  if $SCRATCHPAD
    exe ":Gist " . $SCRATCHPAD
    au! BufLeave,FocusLost * silent! wall
  else
    echoe "No $SCRATCHPAD"
  endif
endfunction
command! OpenScratchpad call s:OpenScratchpad()

" Prepend a header comment with the filename and a dashed line.
function! HeaderComment()
  " Prepend the buffer with filename and a dashed line.
  call append(0, [expand('%:t'), repeat('-', strlen(expand('%:t')))])
  " Comment it out with TComment
  norm ggVjgc
endfunction

" Open a URL using a system-appropriate opener
function! OpenURL(url)
  let url = a:url
  if url !~ "http://"
    let url = "http://" . url
  endif

  " Mac?
  if system("uname") =~ "Darwin" " Mac ?
    silent call system('open "' . url . '" &')
  " Windows?
  elseif has("win32") || has("win64")
    silent call system("start " . url)
  " Some *nix? Does it have xdg-open?
  elseif has("unix") && executable("xdg-open")
    silent call system("xdg-open " . url . " &")
  else
    echoe "Couldn't find a suitable url opener."
  endif

  redraw!
endfunction
command! -nargs=1 OpenURL call OpenURL(<f-args>)

" Open a URL that's under the cursor or visually selected
function! OpenURLAtCursor()
  if mode() ==# 'n'
    let uri = expand("<cWORD>")
    if match(uri, "://")
      let uri = expand("<cfile>")
    endif
  else
    let uri = s:GetSelectedText()
  endif

  call OpenURL(uri)
endfunction
command! -bar OpenURLAtCursor call OpenURLAtCursor(<f-args>)

function! PreviewMarkdown()
  let tempdir = substitute(system("mktemp -d /tmp/vim-md.XXXX"), "\n", "", "")
  let tempfile = tempdir . "/" . expand("%:t:r") . ".html"
  let convertCmd = "pandoc -o " . tempfile . " -c ~/repos/scratchpad/style.css " . expand("%")
  let openCmd = "open " .  tempfile
  silent call system(convertCmd . ";" . openCmd)
endfunction

function! s:GetSelectedText()
  let [lnum1, col1] = getpos("'<")[1:2]
  let [lnum2, col2] = getpos("'>")[1:2]
  let lines = getline(lnum1, lnum2)
  let lines[-1] = lines[-1][: col2 - (&selection == 'inclusive' ? 1 : 2)]
  let lines[0] = lines[0][col1 - 1:]
  return join(lines, "\n")
endfunction

function! s:VSetSearch()
  let temp = @s
  norm! gv"sy
  let @/ = '\V' . substitute(escape(@s, '/\'), '\n', '\\n', 'g') 
  let @s = temp
endfunction

" }}}


" Statusline ----------------------------------------------------- {{{

set statusline=[%n]\ %<%.99f\ %h%w%m%r%{fugitive#statusline()}%{SyntasticStatuslineFlag()}%{exists('*CapsLockStatusline')?CapsLockStatusline():''}%y%=%-16(\ %l,%c-%v\ %)%P

" }}}


" Misc ----------------------------------------------------------- {{{

" Highlight lines longer than 80 chars
highlight OverLength ctermbg=red ctermfg=white guibg=#592929
match OverLength /\%81v.\+/

" VCS conflict markers
match ErrorMsg '^\(<\|=\|>\)\{7\}\([^=].\+\)\?$'

" }}}


" Abbreviations -------------------------------------------------- {{{

iabbrev @@ ndreynolds@gmail.com
iabbrev ndr Nick Reynolds
iabbrev <expr> dts strftime("%c")

" }}}


" Event Autocommands --------------------------------------------- {{{

" Resize splits on window resize
au VimResized * :wincmd =

" Set syntax for a few template extensions
au BufNewFile,BufRead *.jst set syntax=jst
au BufNewFile,BufRead *.json set filetype=json
au BufNewFile,BufRead *.tpl set filetype=html.twig

au BufNewFile,BufRead *.txt set filetype=markdown

" JSON
au BufNewFile,BufRead *.json set filetype=javascript

" Header files should assume C (most likely for me)
au BufNewFile,BufRead *.h set filetype=c

au QuickFixCmdPost *grep* cwindow


" }}}


" FileType Autocommands ------------------------------------------ {{{

augroup FTOptions
  autocmd Filetype python                   setlocal ai et sta sw=4 sts=4 tw=79
  autocmd Filetype cpp,cs,java              setlocal ai et sta sw=4 sts=4 cin
  autocmd Filetype c,ruby,coffee,javascript setlocal ai et sta sw=2 sts=2
  autocmd Filetype css,scss,less            setlocal ai et sta sw=2 sts=2
  autocmd Filetype jst,eruby,eco,haml       setlocal ai et sta sw=2 sts=2
  autocmd Filetype html,xhtml               setlocal ai et sta sw=2 sts=2
  autocmd Filetype html.twig,jade           setlocal ai et sta sw=2 sts=2
  autocmd Filetype eruby,yaml,json          setlocal ai et sta sw=2 sts=2
  autocmd Filetype cucumber                 setlocal ai et sta sw=2 sts=2
  autocmd Filetype vim,markdown             setlocal ai et sta sw=2 sts=2
  autocmd Filetype sh,bash,zsh              setlocal ai et sta sw=2 sts=2
  autocmd FileType gitcommit,markdown       setlocal spell
  autocmd FileType ruby,haml                setlocal spell
  autocmd FileType haskell                  setlocal omnifunc=necoghc#omnifunc
  autocmd FileType crontab                  set nobackup nowritebackup
  autocmd FileType help                     nnoremap <silent><buffer> q :q<cr>

  autocmd FileType ruby,eruby set omnifunc=rubycomplete#Complete
  autocmd FileType ruby,eruby let g:rubycomplete_buffer_loading = 1 
  autocmd FileType ruby,eruby let g:rubycomplete_classes_in_global = 1
  autocmd FileType ruby,eruby let g:rubycomplete_rails = 1
augroup END

" }}}
